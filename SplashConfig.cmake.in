# only add PUBLIC dependencies as well
#   https://cmake.org/cmake/help/latest/manual/cmake-packages.7.html#creating-a-package-configuration-file
include(CMakeFindDependencyMacro)

set(Splash_HAVE_MPI @Splash_HAVE_MPI@)
set(Splash_HAVE_PARALLEL @Splash_HAVE_PARALLEL@)
set(Splash_HAVE_COLLECTIVE @Splash_HAVE_COLLECTIVE@)
set(Splash_HAVE_TOOLS @Splash_HAVE_TOOLS@)

find_dependency(HDF5)

if(Splash_HAVE_MPI)
    find_dependency(MPI)
endif()

set(Splash_MPI_FOUND @Splash_HAVE_MPI@)
set(Splash_PARALLEL_FOUND @Splash_HAVE_PARALLEL@)
set(Splash_COLLECTIVE_FOUND @Splash_HAVE_COLLECTIVE@)
set(Splash_TOOLS_FOUND @Splash_HAVE_TOOLS@)

include("${CMAKE_CURRENT_LIST_DIR}/SplashTargets.cmake")

# check if components are fulfilled and set Splash_<COMPONENT>_FOUND vars
foreach(comp ${Splash_FIND_COMPONENTS})
    if(NOT Splash_${comp}_FOUND)
        if(Splash_FIND_REQUIRED_${comp})
            set(Splash_FOUND FALSE)
        endif()
    endif()
endforeach()
